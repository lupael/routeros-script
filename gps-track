#!rsc by RouterOS
# RouterOS script: gps-track
# Copyright (c) 2018-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# track gps data by sending json data to http server
# https://git.eworm.de/cgit/routeros-scripts/about/doc/gps-track.md

:global GpsTrackUrl;
:global Identity;

:global LogPrintExit;

:local CoordinateFormat [ /system gps get coordinate-format ];
:local Gps [ / system gps monitor once as-value ];

if ($Gps->"valid" = true) do={
  :tool fetch check-certificate=yes-without-crl \
    $GpsTrackUrl output=none \
    http-method=post http-header-field="Content-Type: application/json" \
    http-data=("{" . \
      "\"lat\":\"" . ($Gps->"latitude") . "\"," . \
      "\"lon\":\"" . ($Gps->"longitude") . "\"," . \
      "\"identity\":\"" . $Identity . "\"" . \
    "}");
  $LogPrintExit debug ("Sending GPS data in " . $CoordinateFormat . " format: " . \
    "lat: " . ($Gps->"latitude") . " " . \
    "lon: " . ($Gps->"longitude")) false;
} else={
  $LogPrintExit debug ("GPS data not valid.") false;
}
