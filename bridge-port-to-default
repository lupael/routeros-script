#!rsc by RouterOS
# RouterOS script: bridge-port-to-default
# Copyright (c) 2013-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# reset bridge ports to default bridge
# https://git.eworm.de/cgit/routeros-scripts/about/doc/bridge-port.md

:global BridgePortTo;

:global IfThenElse;
:global LogPrintExit;
:global ParseKeyValueStore;

:foreach BridgePort in=[ / interface bridge port find where !(comment=[]) ] do={
  :local BridgePortVal [ / interface bridge port get $BridgePort ];
  :foreach Config,BridgeDefault in=[ $ParseKeyValueStore ($BridgePortVal->"comment") ] do={
    :if ($Config = $BridgePortTo) do={
      :local DHCPClient [ / ip dhcp-client find where interface=$BridgePortVal->"interface" comment="toggle with bridge port" ];

      :if ($BridgeDefault = "dhcp-client") do={
        :if ([ :len $DHCPClient ] != 1) do={
          $LogPrintExit warning ([ $IfThenElse ([ :len $DHCPClient ] = 0) "Missing" "Duplicate" ] . \
              " dhcp client configuration for interface " . $BridgePortVal->"interface" . "!") true;
        }
        :local DHCPClientDisabled [ / ip dhcp-client get $DHCPClient disabled ];

        :if ($BridgePortVal->"disabled" = false || $DHCPClientDisabled = true) do={
          $LogPrintExit info ("Disabling bridge port for interface " . $BridgePortVal->"interface" . ", enabling dhcp client.") false;
          / interface bridge port disable $BridgePort;
          / ip dhcp-client enable $DHCPClient;
        }
      } else={
        :if ($BridgePortVal->"disabled" = true || $BridgeDefault != $BridgePortVal->"bridge") do={
          $LogPrintExit info ("Enabling bridge port for interface " . $BridgePortVal->"interface" . ", changing to " . $BridgePortTo . \
              " bridge " . $BridgeDefault . ", disabling dhcp client.") false;
          :if ([ :len $DHCPClient ] = 1) do={
            / ip dhcp-client disable $DHCPClient;
            :delay 200ms;
          }
          / interface bridge port set disabled=no bridge=$BridgeDefault $BridgePort;
        } else={
          $LogPrintExit debug ("Interface " . $BridgePortVal->"interface" . " already connected to " . $BridgePortTo . \
              " bridge " . $BridgeDefault . ".") false;
        }
      }
    }
  }
}
