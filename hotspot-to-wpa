#!rsc by RouterOS
# RouterOS script: hotspot-to-wpa
# Copyright (c) 2019-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# add private WPA passphrase after hotspot login
# https://git.eworm.de/cgit/routeros-scripts/about/doc/hotspot-to-wpa.md

:global LogPrintExit;

:local MacAddress $"mac-address";
:local UserName $username;
:local Date [ / system clock get date ];
:local PassWord [ / ip hotspot user get [ find where name=$UserName ] password ];

:if ([ :len [ / caps-man access-list find where comment="--- hotspot-to-wpa above ---" disabled ] ] = 0) do={
  / caps-man access-list add comment="--- hotspot-to-wpa above ---" disabled=yes;
  $LogPrintExit warning "Added disabled access-list entry with comment '--- hotspot-to-wpa above ---'." false;
}
:local PlaceBefore ([ / caps-man access-list find where comment="--- hotspot-to-wpa above ---" disabled ]->0);

$LogPrintExit info ("Adding/updating accesslist entry for mac address " . $MacAddress . \
  " (user " . $UserName . ").") false;

/ caps-man access-list remove [ find where mac-address=$MacAddress comment~"^hotspot-to-wpa: " ];

:local Limits [ / caps-man access-list get $PlaceBefore ];
:if (($Limits->"ap-tx-limit") > 0 && ($Limits->"client-tx-limit") > 0) do={
  / caps-man access-list add comment=("hotspot-to-wpa: " . $UserName . ", " . $MacAddress . ", " . $Date) \
      mac-address=$MacAddress private-passphrase=$PassWord ssid-regexp="-wpa\$" \
      ap-tx-limit=($Limits->"ap-tx-limit") client-tx-limit=($Limits->"client-tx-limit") \
      place-before=$PlaceBefore;
} else={
  / caps-man access-list add comment=("hotspot-to-wpa: " . $UserName . ", " . $MacAddress . ", " . $Date) \
      mac-address=$MacAddress private-passphrase=$PassWord ssid-regexp="-wpa\$" place-before=$PlaceBefore;
}
